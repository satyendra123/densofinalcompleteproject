// working turnstile controller
#include <SPI.h>
#include <Ethernet.h>
#include <Wiegand.h>

byte mac[] = { 0xDE, 0xAD, 0xBE, 0xEF, 0xFE, 0x01 };
IPAddress ip(192, 168, 1, 16);
IPAddress serverIp(192, 168, 1, 100);

const int RELAY_PIN = 9;

#define D0_PIN 2
#define D1_PIN 3
const unsigned long RFID_RESET_TIMEOUT = 5000;

EthernetClient client;
WIEGAND wg;

String lastRFID = "";
unsigned long lastRFIDTime = 0;

void setup() {
  Serial.begin(9600);

  pinMode(RELAY_PIN, OUTPUT);
  digitalWrite(RELAY_PIN, HIGH);

  Ethernet.begin(mac, ip);
  delay(1000);
  Serial.println("Ethernet initialized");
  wg.begin(D0_PIN, D1_PIN);
}

void loop() {

  if (wg.available()) {
    uint32_t cardCode = wg.getCode();
    String currentRFID = String(cardCode);
    Serial.println("Scanned RFID: " + currentRFID);

    if (currentRFID != lastRFID) {
      lastRFID = currentRFID;
      lastRFIDTime = millis();
      sendRFIDToServer(currentRFID);
    } else {
      Serial.println("Same RFID, not sending");
    }
  }

  if (lastRFID != "" && (millis() - lastRFIDTime >= RFID_RESET_TIMEOUT)) {
    Serial.println("Clearing lastRFID...");
    lastRFID = "";
  }
}

void sendRFIDToServer(String rfid) {
  Serial.println("Sending to server: " + rfid);

  if (client.connect(serverIp, 8000)) {
    String url = "GET /rfid?rfid=" + rfid + " HTTP/1.1";
    client.println(url);
    client.println("Host: 192.168.1.100");
    client.println("Connection: close");
    client.println();

    while (client.connected()) {
      if (client.available()) {
        String line = client.readStringUntil('\n');
        Serial.println("Server Response: " + line);

        if (line.indexOf("|OPENEN%") >= 0) {
          triggerGate();
        }
      }
    }

    client.stop();
  } else {
    Serial.println("Connection failed");
  }
}

void triggerGate() {
  Serial.println("Gate Triggered!");
  digitalWrite(RELAY_PIN, LOW);
  delay(500);
  digitalWrite(RELAY_PIN, HIGH);
  Serial.println("Gate closed.");
}
